task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

ext {
    springVersion = '4.0.0.RELEASE'
    springDataJpaVersion = '1.4.1.RELEASE'
    hibernateVersion = '4.3.0.Final'
    junitVersion = '4.11'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    version = '0.1-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
        maven {
            url 'http://repo.spring.io/milestone'
        }
    }

    dependencies {
        compile 'org.slf4j:slf4j-log4j12:1.7.5'
        testCompile "junit:junit:$junitVersion"
        testCompile "org.springframework:spring-test:$springVersion"
        testCompile 'com.jayway.jsonpath:json-path:0.8.1'
        testCompile 'org.mockito:mockito-all:1.9.0'
        testCompile 'org.kubek2k:springockito-annotations:1.0.9'
    }
}

project(':common') {
    apply plugin: 'groovy'

    dependencies {
        compile 'com.google.guava:guava:15.0'
        compile 'commons-beanutils:commons-beanutils:1.8.3'
    }
}

project(':dao-api') {
    dependencies {
        compile("org.springframework.data:spring-data-jpa:$springDataJpaVersion") {
            exclude group: 'org.springframework'
        }
        compile "org.hibernate:hibernate-entitymanager:$hibernateVersion"
    }
}

project(':dao-impl') {
    dependencies {
        compile project(':common')
        compile project(':dao-api')
        compile "org.springframework:spring-context:$springVersion"
        compile "org.springframework:spring-orm:$springVersion"
        compile 'org.hsqldb:hsqldb:2.0.0'
    }
}

project(':service-api') {
    dependencies {
        compile project(':dao-api')
    }
}

project(':service-impl') {
    apply plugin: 'groovy'

    dependencies {
        compile project(':common')
        compile project(':service-api')
        compile project(':dao-impl')
    }
}

project(':ws-api') {
}

project(':ws-impl') {
    apply plugin: 'groovy'

    sourceSets.main.java.srcDirs = []
    sourceSets.main.groovy.srcDirs += ['src/main/java']

    dependencies {
        compile localGroovy()
        compile project(':common')
        compile project(':ws-api')
        compile project(':service-impl')
    }
}

project(':web') {
    apply plugin: 'war'
    apply plugin: 'groovy'

    sourceSets.main.java.srcDirs = []
    sourceSets.main.groovy.srcDirs += ['src/main/java']

    dependencies {
        compile localGroovy()
        compile project(':common')
//        compile project(':ws-api')
        compile project(':ws-impl')
        compile "org.springframework:spring-webmvc:$springVersion"
        compile "javax.servlet:javax.servlet-api:3.0.1"
        compile "org.codehaus.jackson:jackson-mapper-asl:1.9.13"
    }
}
